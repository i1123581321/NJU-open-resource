# FUNCTIONAL PROGRAMMING IN COQ

## Data and Functions

### Enumerate types

可以通过如下方式定义用户类型

```
Inductive day : Type :=
  | monday
  | tuesday
  | wednesday
  | thursday
  | friday
  | saturday
  | sunday.
```

然后定义其上的操作

```
Definition next_weekday (d:day) : day :=
  match d with
  | monday ⇒ tuesday
  | tuesday ⇒ wednesday
  | wednesday ⇒ thursday
  | thursday ⇒ friday
  | friday ⇒ monday
  | saturday ⇒ monday
  | sunday ⇒ monday
  end.
```

然后通过 compute 命令计算结果

```
Compute (next_weekday friday).
```

也可以通过写断言的方式

```
Example test_next_weekday:
  (next_weekday (next_weekday saturday)) = tuesday.
```

然后通过 coq 来验证

```
Proof. simpl. reflexivity. Qed.
```

> this can be read as "The assertion we've just made can be proved by observing that both sides of the equality evaluate to the same thing."

`Notation` 命令可以为现有的定义再赋一个符号

```
Notation "x && y" := (andb x y).
Notation "x || y" := (orb x y).
```

> 对于 notation，可以定义其优先级和结合性
>
> ```
> Notation "x + y" := (plus x y)
>                        (at level 50, left associativity)
>                        : nat_scope.
> ```
>
> 每个 notation 还有一个 notation scope

`Check` 命令可以检查当前表达式的类型，如果表达式后有冒号和一个类型，coq 会检查表达式和类型是否匹配

```
Check true
    : bool.
```

函数同样有类型

上述定义的都是枚举的类型，定义就是枚举有限集上的元素，每个枚举称为一个 constructor，而有的 constructor 可以接受参数

```
Inductive rgb : Type :=
  | red
  | green
  | blue.
Inductive color : Type :=
  | black
  | white
  | primary (p : rgb).
```

> constructor 的集合是无限的，将 constructor 应用与 0 个或多个 constructor 或者 constructor expression 可以得到 construction expression
>
> `Inductive` 关键字定义了整个 constructor expression 空间内的一个子集，并将其命名

在定义操作时，对于有参数的 constructor 要匹配其参数

```
Definition isred (c : color) : bool :=
  match c with
  | black ⇒ false
  | white ⇒ false
  | primary red ⇒ true
  | primary _ ⇒ false
  end.
```

### Module

coq 同样有 module，以在大型的开发中组织代码，类似名空间

```
Module Playground.
  Definition b : rgb := blue.
End Playground.
Definition b : bool := true.
Check Playground.b : rgb.
Check b : bool.
```

有多个参数的 constructor 可以用来构造 tuple 类型，可以用 pattern-matching 来解包参数

### Number

上述定义的类型都是有限的，可以定义无限的类型（如自然数）

```
Inductive nat : Type :=
  | O
  | S (n : nat).
```

递归定义

coq 内置了语法糖来提高这种定义的可读性

```
Check (S (S (S (S O)))).
(* ===> 4 : nat *)
```

定义带有递归的函数需要用到关键字 `Fixpoint`

```
Fixpoint evenb (n:nat) : bool :=
  match n with
  | O ⇒ true
  | S O ⇒ false
  | S (S n') ⇒ evenb n'
  end.
```

定义递归函数需要函数的参数递减，这样保证不会无限循环

在参数类型相同时，可以将其写在一起。在进行匹配时，如果两个 pattern 有相同的处理，则可以用逗号连接起来

```
Fixpoint minus (n m:nat) : nat :=
  match n, m with
  | O , _ ⇒ O
  | S _ , O ⇒ n
  | S n', S m' ⇒ minus n' m'
  end.
```

## Proof

### Proof by Simplification

上述的证明都是通过 `Example` 来声明某些函数在特定的输入下的输出，然后通过 `simpl` 来简化两边的表达式，然后通过 `reflexivity` 来检查等号两边是否相等

类似“任何数加 0 都等于其本身”的性质也可以表达出来

```
Theorem plus_0_n: forall n : nat, 0 + n = n.
Proof.
	intros n. simpl. reflexivity. Qed.
```

`reflexivity` 会进行一些简单的简化和展开，但是如果证明正确的话不会显示被展开的结果，如果需要化简中间过程的话使用 `simpl.`

使用 `Theorem` 还是 `Example`（或者 `Lemma`，`Fact`，`Remark`）对 coq 没有多少区别，只是一种风格上的选择

上述证明还加了谓词 `forall`，证明从“设 n 是某自然数开始”，即 `intros n.` ，这句命令将 goal 中的 n 引入证明的上下文，也可以使用别的标识符，但是会带来混乱。

诸如 `intros/simpl/reflexivity` 都是 tactic，即在 `Proof` 和 `Qed` 之间用于指示证明过程的语句

### Proof by Rewriting

考虑如下性质的定理

```
Theorem plus_id_example : ∀ n m:nat,
  n = m ->
  n + n = m + m.
```

针对 `n=m` 的特殊情况。如果要证明需要将 `n,m` 以及 `n=m` 这个假说均引入 context

```
Proof.
  (* move both quantifiers into the context: *)
  intros n m.
  (* move the hypothesis into the context: *)
  intros H.
  (* rewrite the goal using the hypothesis: *)
  rewrite -> H.
  reflexivity. Qed.
```

`rewrite` 用假说中的等式将 `n` 替换为 `m`，然后用 `reflexivity` 证明

如果要从右向左 rewrite，将 `->` 改成 `<-`

### Proof by Case Analysis

化简和 rewrite 不能解决复杂的问题，如

```
Theorem plus_1_neq_0_firsttry : ∀ n : nat,
  (n + 1) =? 0 = false.
Proof.
  intros n.
  simpl. (* does nothing! *)
Abort.
```

不能化简的原因是 + 和 =? 都需要 match，而参数 `n` 此时是未知的，需要分情况考虑，使用 `destruct` 关键字

```
Theorem plus_1_neq_0 : ∀ n : nat,
  (n + 1) =? 0 = false.
Proof.
  intros n. destruct n as [| n'] eqn:E.
  - reflexivity.
  - reflexivity. Qed.
```

`destruct` 将 goal 分情况为两个 subgoal，`n = O/n = S n'`， `as [| n']` 称为 intro pattern，指明在每个 subgoal 中引入的变量名，用 `|` 分隔开，第一个 name list 为空是因为 `O` 是个零元构造符

`eqn:E` 将上述等式命名为 `E`

`-` 称为 bullets，对应每个生成的 subgoal，在之后是证明相应 subgoal 的过程，上述命题的 subgoal 用简单的 `reflexivity` 即可证明

在证明 subgoal 时可以继续使用 `destruct`，此时需要换一个 bullet 来标记不同级别的 subgoal

可以用各种 `- + *` 来代替 bullet，或是用嵌套的花括号